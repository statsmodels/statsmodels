#cython: boundscheck=False
#cython: wraparound=False
#cython: cdivision=False
"""
State Space Models

Author: Chad Fulton  
License: Simplified-BSD
"""

{{py:

TYPES = {
    "s": ("np.float32_t", "np.float32", "np.NPY_FLOAT32"),
    "d": ("np.float64_t", "float", "np.NPY_FLOAT64"),
    "c": ("np.complex64_t", "np.complex64", "np.NPY_COMPLEX64"),
    "z": ("np.complex128_t", "complex", "np.NPY_COMPLEX128"),
}

}}

# Typical imports
import numpy as np
import warnings
cimport numpy as np
cimport cython

np.import_array()

from statsmodels.src.math cimport *
cimport scipy.linalg.cython_blas as blas
cimport scipy.linalg.cython_lapack as lapack
cimport statsmodels.tsa.statespace._tools as tools

{{for prefix, types in TYPES.items()}}
from statsmodels.tsa.statespace._initialization cimport {{prefix}}Initialization
{{endfor}}

cdef int FORTRAN = 1

{{for prefix, types in TYPES.items()}}
{{py:cython_type, dtype, typenum = types}}
{{py:
combined_prefix = prefix
combined_cython_type = cython_type
if prefix == 'c':
    combined_prefix = 'z'
    combined_cython_type = 'np.complex128_t'
if prefix == 's':
    combined_prefix = 'd'
    combined_cython_type = 'np.float64_t'
}}

## State Space Representation
cdef class {{prefix}}Statespace(object):
    """
    {{prefix}}Statespace(obs, design, obs_intercept, obs_cov, transition, state_intercept, selection, state_cov)

    *See Durbin and Koopman (2012), Chapter 4 for all notation*
    """

    # ### State space representation
    # 
    # $$
    # \begin{align}
    # y_t & = Z_t \alpha_t + d_t + \varepsilon_t \hspace{3em} & \varepsilon_t & \sim N(0, H_t) \\\\
    # \alpha_{t+1} & = T_t \alpha_t + c_t + R_t \eta_t & \eta_t & \sim N(0, Q_t) \\\\
    # & & \alpha_1 & \sim N(a_1, P_1)
    # \end{align}
    # $$
    # 
    # $y_t$ is $p \times 1$  
    # $\varepsilon_t$ is $p \times 1$  
    # $\alpha_t$ is $m \times 1$  
    # $\eta_t$ is $r \times 1$  
    # $t = 1, \dots, T$

    # `nobs` $\equiv T$ is the length of the time-series  
    # `k_endog` $\equiv p$ is dimension of observation space  
    # `k_states` $\equiv m$ is the dimension of the state space  
    # `k_posdef` $\equiv r$ is the dimension of the state shocks  
    # *Old notation: T, n, k, g*
    # cdef readonly int nobs, k_endog, k_states, k_posdef
    
    # `obs` $\equiv y_t$ is the **observation vector** $(p \times T)$  
    # `design` $\equiv Z_t$ is the **design vector** $(p \times m \times T)$  
    # `obs_intercept` $\equiv d_t$ is the **observation intercept** $(p \times T)$  
    # `obs_cov` $\equiv H_t$ is the **observation covariance matrix** $(p \times p \times T)$  
    # `transition` $\equiv T_t$ is the **transition matrix** $(m \times m \times T)$  
    # `state_intercept` $\equiv c_t$ is the **state intercept** $(m \times T)$  
    # `selection` $\equiv R_t$ is the **selection matrix** $(m \times r \times T)$  
    # `state_cov` $\equiv Q_t$ is the **state covariance matrix** $(r \times r \times T)$  
    # `selected_state_cov` $\equiv R Q_t R'$ is the **selected state covariance matrix** $(m \times m \times T)$  
    # `initial_state` $\equiv a_1$ is the **initial state mean** $(m \times 1)$  
    # `initial_state_cov` $\equiv P_1$ is the **initial state covariance matrix** $(m \times m)$
    # `initial_state_cov` $\equiv P_\inf$ is the **initial diffuse state covariance matrix** $(m \times m)$
    #
    # With the exception of `obs`, these are *optionally* time-varying. If they are instead time-invariant,
    # then the dimension of length $T$ is instead of length $1$.
    #
    # *Note*: the initial vectors' notation 1-indexed as in Durbin and Koopman,
    # but in the recursions below it will be 0-indexed in the Python arrays.
    # 
    # *Old notation: y, -, mu, beta_tt_init, P_tt_init*
    # cdef readonly {{cython_type}} [::1,:] obs, obs_intercept, state_intercept
    # cdef readonly {{cython_type}} [:] initial_state
    # cdef readonly {{cython_type}} [::1,:] initial_state_cov
    # *Old notation: H, R, F, G, Q*, G Q* G'*
    # cdef readonly {{cython_type}} [::1,:,:] design, obs_cov, transition, selection, state_cov, selected_state_cov

    # `missing` is a $(p \times T)$ boolean matrix where a row is a $(p \times 1)$ vector
    # in which the $i$th position is $1$ if $y_{i,t}$ is to be considered a missing value.  
    # *Note:* This is created as the output of np.isnan(obs).
    # cdef readonly int [::1,:] missing
    # `nmissing` is an `T \times 0` integer vector holding the number of *missing* observations
    # $p - p_t$
    # cdef readonly int [:] nmissing

    # Flag for a time-invariant model, which requires that *all* of the
    # possibly time-varying arrays are time-invariant.
    # cdef readonly int time_invariant

    # Flag for initialization.
    # cdef readonly int initialized

    # Flags for performance improvements
    # TODO need to add this to the UI in representation
    # cdef public int diagonal_obs_cov
    # cdef public int subset_design
    # cdef public int companion_transition

    # Temporary arrays
    # cdef {{cython_type}} [::1,:] tmp

    # Temporary selection arrays
    # cdef readonly {{cython_type}} [:] selected_obs
    # The following are contiguous memory segments which are then used to
    # store the data in the above matrices.
    # cdef readonly {{cython_type}} [:] selected_design
    # cdef readonly {{cython_type}} [:] selected_obs_cov

    # Temporary transformation arrays
    # cdef readonly {{cython_type}} [::1,:] transform_cholesky
    # cdef readonly {{cython_type}} [::1,:] transform_obs_cov
    # cdef readonly {{cython_type}} [::1,:] transform_design
    # cdef readonly {{cython_type}} transform_determinant

    # cdef readonly {{cython_type}} [:] collapse_obs
    # cdef readonly {{cython_type}} [:] collapse_obs_tmp
    # cdef readonly {{cython_type}} [::1,:] collapse_design
    # cdef readonly {{cython_type}} [::1,:] collapse_obs_cov
    # cdef readonly {{cython_type}} [::1,:] collapse_cholesky
    # cdef readonly {{cython_type}} collapse_loglikelihood

    # Pointers  
    # cdef {{cython_type}} * _obs
    # cdef {{cython_type}} * _design
    # cdef {{cython_type}} * _obs_intercept
    # cdef {{cython_type}} * _obs_cov
    # cdef {{cython_type}} * _transition
    # cdef {{cython_type}} * _state_intercept
    # cdef {{cython_type}} * _selection
    # cdef {{cython_type}} * _state_cov
    # cdef {{cython_type}} * _selected_state_cov
    # cdef {{cython_type}} * _initial_state
    # cdef {{cython_type}} * _initial_state_cov

    # Current location dimensions
    # cdef int _k_endog, _k_states, _k_posdef, _k_endog2, _k_states2, _k_posdef2, _k_endogstates, _k_statesposdef
    # cdef int _nmissing

    # ### Initialize state space model
    # *Note*: The initial state and state covariance matrix must be provided.
    def __init__(self,
                 {{cython_type}} [::1,:]   obs,
                 {{cython_type}} [::1,:,:] design,
                 {{cython_type}} [::1,:]   obs_intercept,
                 {{cython_type}} [::1,:,:] obs_cov,
                 {{cython_type}} [::1,:,:] transition,
                 {{cython_type}} [::1,:]   state_intercept,
                 {{cython_type}} [::1,:,:] selection,
                 {{cython_type}} [::1,:,:] state_cov,
                 diagonal_obs_cov=-1):

        # Local variables
        cdef:
            int t, i, j
            np.npy_intp dim1[1]
            np.npy_intp dim2[2]
            np.npy_intp dim3[3]

        # #### State space representation variables  
        # **Note**: these arrays share data with the versions defined in
        # Python and passed to this constructor, so if they are updated in
        # Python they will also be updated here.
        self.obs = obs
        self.design = design
        self.obs_intercept = obs_intercept
        self.obs_cov = obs_cov
        self.transition = transition
        self.state_intercept = state_intercept
        self.selection = selection
        self.state_cov = state_cov

        # Dimensions
        self.k_endog = obs.shape[0]
        self.k_states = selection.shape[0]
        self.k_posdef = selection.shape[1]
        self.nobs = obs.shape[1]

        # #### Validate matrix dimensions
        #
        # Make sure that the given state-space matrices have consistent sizes
        tools.validate_matrix_shape('design', &self.design.shape[0],
                              self.k_endog, self.k_states, self.nobs)
        tools.validate_vector_shape('observation intercept', &self.obs_intercept.shape[0],
                              self.k_endog, self.nobs)
        tools.validate_matrix_shape('observation covariance matrix', &self.obs_cov.shape[0],
                              self.k_endog, self.k_endog, self.nobs)
        tools.validate_matrix_shape('transition', &self.transition.shape[0],
                              self.k_states, self.k_states, self.nobs)
        tools.validate_vector_shape('state intercept', &self.state_intercept.shape[0],
                              self.k_states, self.nobs)
        tools.validate_matrix_shape('state covariance matrix', &self.state_cov.shape[0],
                              self.k_posdef, self.k_posdef, self.nobs)

        # Check for a time-invariant model
        self.time_invariant = (
            self.design.shape[2] == 1           and
            self.obs_intercept.shape[1] == 1    and
            self.obs_cov.shape[2] == 1          and
            self.transition.shape[2] == 1       and
            self.state_intercept.shape[1] == 1  and
            self.selection.shape[2] == 1        and
            self.state_cov.shape[2] == 1
        )

        # Set the flag for initialization to be false
        self.initialized = False

        self.diagonal_obs_cov = diagonal_obs_cov
        self._diagonal_obs_cov = -1

        # Allocate selected state covariance matrix
        dim3[0] = self.k_states; dim3[1] = self.k_states; dim3[2] = 1;
        # (we only allocate memory for time-varying array if necessary)
        if self.state_cov.shape[2] > 1 or self.selection.shape[2] > 1:
            dim3[2] = self.nobs
        self.selected_state_cov = np.PyArray_ZEROS(3, dim3, {{typenum}}, FORTRAN)

        # Handle missing data
        self.missing = np.array(np.isnan(obs), dtype=np.int32, order="F")
        self.nmissing = np.array(np.sum(self.missing, axis=0), dtype=np.int32)
        self.has_missing = np.sum(self.nmissing) > 0

        # Create the temporary array
        # Holds arrays of dimension $(m \times m)$
        dim2[0] = self.k_states; dim2[1] = max(self.k_states, self.k_posdef);
        self.tmp = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)

        # Arrays for initialization
        dim1[0] = self.k_states;
        self.initial_state = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        dim2[0] = self.k_states; dim2[1] = self.k_states;
        self.initial_state_cov = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)
        dim2[0] = self.k_states; dim2[1] = self.k_states;
        self.initial_diffuse_state_cov = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)

        # Arrays for missing data
        dim1[0] = self.k_endog;
        self.selected_obs = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        dim1[0] = self.k_endog;
        self.selected_obs_intercept = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        dim1[0] = self.k_endog * self.k_states;
        self.selected_design = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        dim1[0] = self.k_endog**2;
        self.selected_obs_cov = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)

        # Arrays for transformations
        dim2[0] = self.k_endog; dim2[1] = self.k_endog;
        self.transform_cholesky = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)
        self.transform_obs_cov = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)
        dim2[0] = self.k_endog; dim2[1] = self.k_states;
        self.transform_design = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)
        dim1[0] = self.k_endog;
        self.transform_obs_intercept = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)

        dim1[0] = self.k_states;
        self.collapse_obs = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        self.collapse_obs_tmp = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        dim2[0] = self.k_states; dim2[1] = self.k_states;
        self.collapse_design = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)
        self.collapse_obs_cov = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)
        self.collapse_cholesky = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)

        # Initialize location
        self.t = 0
        self._previous_t = 0

        # Initialize dimensions
        self.set_dimensions(self.k_endog, self.k_states, self.k_posdef)

    def __reduce__(self):
        init = (np.array(self.obs, copy=True, order='F'), np.array(self.design, copy=True, order='F'),
                np.array(self.obs_intercept, copy=True, order='F'), np.array(self.obs_cov, copy=True, order='F'),
                np.array(self.transition, copy=True, order='F'), np.array(self.state_intercept, copy=True, order='F'),
                np.array(self.selection, copy=True, order='F'), np.array(self.state_cov, copy=True, order='F'),
                self.diagonal_obs_cov)
        state = {'initialized': self.initialized,
                 'initial_state': None,
                 'initial_state_cov': None,
                 'initial_diffuse_state_cov': None,
                 'missing': np.array(self.missing, copy=True, order='F'),
                 'nmissing': np.array(self.nmissing, copy=True, order='F'),
                 'has_missing': self.has_missing,
                 'tmp': np.array(self.tmp, copy=True, order='F'),
                 'selected_state_cov': np.array(self.selected_state_cov, copy=True, order='F'),
                 'selected_obs': np.array(self.selected_obs, copy=True, order='F'),
                 'selected_obs_intercept': np.array(self.selected_obs_intercept, copy=True, order='F'),
                 'selected_design': np.array(self.selected_design, copy=True, order='F'),
                 'selected_obs_cov': np.array(self.selected_obs_cov, copy=True, order='F'),
                 'transform_cholesky': np.array(self.transform_cholesky, copy=True, order='F'),
                 'transform_obs_cov': np.array(self.transform_obs_cov, copy=True, order='F'),
                 'transform_design': np.array(self.transform_design, copy=True, order='F'),
                 'collapse_obs': np.array(self.collapse_obs, copy=True, order='F'),
                 'collapse_obs_tmp': np.array(self.collapse_obs_tmp, copy=True, order='F'),
                 'collapse_design': np.array(self.collapse_design, copy=True, order='F'),
                 'collapse_obs_cov': np.array(self.collapse_obs_cov, copy=True, order='F'),
                 'collapse_cholesky': np.array(self.collapse_cholesky, copy=True, order='F'),
                 't': self.t,
                 'collapse_loglikelihood': self.collapse_loglikelihood,
                 'companion_transition': self.companion_transition,
                 'transform_determinant': self.transform_determinant,
                 }
        if self.initialized:
            state['initial_state'] = np.array(self.initial_state, copy=True, order='F')
            state['initial_state_cov'] = np.array(self.initial_state_cov, copy=True, order='F')
            state['initial_diffuse_state_cov'] = np.array(self.initial_diffuse_state_cov, copy=True, order='F')
        return (self.__class__, init, state)

    def __setstate__(self, state):
        self.initial_state = state['initial_state']
        self.initial_state_cov = state['initial_state_cov']
        self.initial_diffuse_state_cov = state['initial_diffuse_state_cov']
        self.initialized = state['initialized']
        self.selected_state_cov = state['selected_state_cov']
        self.missing = state['missing']
        self.nmissing =state['nmissing']
        self.has_missing = state['has_missing']
        self.tmp = state['tmp']
        self.selected_obs  = state['selected_obs']
        self.selected_obs_intercept  = state['selected_obs_intercept']
        self.selected_design  = state['selected_design']
        self.selected_obs_cov  =state['selected_obs_cov']
        self.transform_cholesky  = state['transform_cholesky']
        self.transform_obs_cov  = state['transform_obs_cov']
        self.transform_design = state['transform_design']
        self.collapse_obs = state['collapse_obs']
        self.collapse_obs_tmp = state['collapse_obs_tmp']
        self.collapse_design = state['collapse_design']
        self.collapse_obs_cov = state['collapse_obs_cov']
        self.collapse_cholesky = state['collapse_cholesky']
        self.t = state['t']
        self.collapse_loglikelihood = state['collapse_loglikelihood']
        self.companion_transition = state['companion_transition']
        self.transform_determinant = state['transform_determinant']

    def initialize(self, init, offset=0, complex_step=False, clear=True):
        cdef {{prefix}}Initialization _init
        # Clear initial arrays
        if clear:
            self.initial_state[:] = 0
            self.initial_diffuse_state_cov[:] = 0
            self.initial_state_cov[:] = 0

        # If using global initialization, compute the actual elements and
        # return them
        if init.initialization_type is not None:
            init._initialize_initialization(prefix='{{prefix}}')
            _init = init._initializations['{{prefix}}']
            _init.initialize(init.initialization_type, offset, self,
                             self.initial_state,
                             self.initial_diffuse_state_cov,
                             self.initial_state_cov, complex_step)
        # Otherwise, if using blocks, initialize each of the blocks
        else:
            for block_index, block_init in init.blocks.items():
                self.initialize(block_init, offset=offset + block_index[0],
                                complex_step=complex_step, clear=False)

        if not self.initialized:
            self.initialized = True

    # ## Initialize: known values
    #
    # Initialize the filter with specific values, assumed to be known with
    # certainty or else as filled with parameters from a maximum likelihood
    # estimation run.
    def initialize_known(self, {{cython_type}} [:] initial_state, {{cython_type}} [::1,:] initial_state_cov):
        """
        initialize_known(initial_state, initial_state_cov)
        """
        tools.validate_vector_shape('initial state', &initial_state.shape[0], self.k_states, None)
        tools.validate_matrix_shape('initial state covariance', &initial_state_cov.shape[0], self.k_states, self.k_states, None)

        self.initial_state = initial_state
        self.initial_state_cov = initial_state_cov
        self.initial_diffuse_state_cov[:] = 0

        self.initialized = True

    # ## Initialize: approximate diffuse priors
    #
    # Durbin and Koopman note that this initialization should only be coupled
    # with the standard Kalman filter for "approximate exploratory work" and
    # can lead to "large rounding errors" (p. 125).
    # 
    # *Note:* see Durbin and Koopman section 5.6.1
    def initialize_approximate_diffuse(self, {{cython_type}} variance=1e2):
        """
        initialize_approximate_diffuse(variance=1e2)
        """
        cdef np.npy_intp dim[1]
        dim[0] = self.k_states
        self.initial_state = np.PyArray_ZEROS(1, dim, {{typenum}}, FORTRAN)
        self.initial_state_cov = np.eye(self.k_states, dtype={{dtype}}).T * variance
        self.initial_diffuse_state_cov[:] = 0

        self.initialized = True

    # ## Initialize: stationary process
    # *Note:* see Durbin and Koopman section 5.6.2
    def initialize_stationary(self, complex_step=False):
        """
        initialize_stationary()
        """
        cdef np.npy_intp dim1[1]
        cdef np.npy_intp dim2[2]
        cdef int i, info, inc = 1
        cdef int k_states2 = self.k_states**2
        cdef np.float64_t asum, tol = 1e-9
        cdef {{cython_type}} scalar
        cdef int [::1,:] ipiv

        # Create selected state covariance matrix
        {{prefix}}select_cov(self.k_states, self.k_posdef,
                                   &self.tmp[0,0],
                                   &self.selection[0,0,0],
                                   &self.state_cov[0,0,0],
                                   &self.selected_state_cov[0,0,0])

        # Initial state mean
        {{if combined_prefix == 'd'}}
        asum = blas.{{prefix}}asum(&self.k_states, &self.state_intercept[0, 0], &inc)
        {{elif prefix == 'c'}}
        asum = blas.scasum(&self.k_states, &self.state_intercept[0, 0], &inc)
        {{else}}
        asum = blas.dzasum(&self.k_states, &self.state_intercept[0, 0], &inc)
        {{endif}}

        dim1[0] = self.k_states
        self.initial_state = np.PyArray_ZEROS(1, dim1, {{typenum}}, FORTRAN)
        if asum > tol:
            dim2[0] = self.k_states
            dim2[1] = self.k_states
            ipiv = np.PyArray_ZEROS(2, dim2, np.NPY_INT32, FORTRAN)

            # I - T
            blas.{{prefix}}copy(&k_states2, &self.transition[0,0,0], &inc,
                                            &self.tmp[0,0], &inc)
            scalar = -1.0
            blas.{{prefix}}scal(&k_states2, &scalar, &self.tmp[0, 0], &inc)
            for i in range(self.k_states):
                self.tmp[i, i] = self.tmp[i, i] + 1

            # c
            blas.{{prefix}}copy(&self.k_states, &self.state_intercept[0,0], &inc,
                                                &self.initial_state[0], &inc)

            # Solve (I - T) x = c
            lapack.{{prefix}}getrf(&self.k_states, &self.k_states, &self.tmp[0, 0], &self.k_states,
                                   &ipiv[0, 0], &info)
            lapack.{{prefix}}getrs('N', &self.k_states, &inc, &self.tmp[0, 0], &self.k_states,
                                   &ipiv[0, 0], &self.initial_state[0], &self.k_states, &info)

        dim2[0] = self.k_states; dim2[1] = self.k_states;
        self.initial_state_cov = np.PyArray_ZEROS(2, dim2, {{typenum}}, FORTRAN)

        # Create a copy of the transition matrix (to avoid overwriting it)
        blas.{{prefix}}copy(&k_states2, &self.transition[0,0,0], &inc,
                                   &self.tmp[0,0], &inc)

        # Copy the selected state covariance to the initial state covariance
        # (it will be overwritten with the appropriate matrix)
        blas.{{prefix}}copy(&k_states2, &self.selected_state_cov[0,0,0], &inc,
                                   &self.initial_state_cov[0,0], &inc)

        # Solve the discrete Lyapunov equation to the get initial state
        # covariance matrix
        tools._{{prefix}}solve_discrete_lyapunov(&self.tmp[0,0], &self.initial_state_cov[0,0], self.k_states, complex_step)

        self.initial_diffuse_state_cov[:] = 0

        self.initialized = True

    def __iter__(self):
        return self

    def __next__(self):
        """
        Advance to the next location
        """
        if self.t >= self.nobs:
            raise StopIteration
        else:
            self.seek(self.t+1, 0, 0)

    cpdef seek(self, unsigned int t, unsigned int transform_diagonalize, unsigned int transform_generalized_collapse):
        self._previous_t = self.t

        # Set the global time indicator, if valid
        if t >= self.nobs:
            raise IndexError("Observation index out of range")
        self.t = t

        # Indices for possibly time-varying arrays
        cdef:
            int k_endog
            int design_t = 0
            int obs_intercept_t = 0
            int obs_cov_t = 0
            int transition_t = 0
            int state_intercept_t = 0
            int selection_t = 0
            int state_cov_t = 0

        # Get indices for possibly time-varying arrays
        if not self.time_invariant:
            if self.design.shape[2] > 1:             design_t = t
            if self.obs_intercept.shape[1] > 1:      obs_intercept_t = t
            if self.obs_cov.shape[2] > 1:            obs_cov_t = t
            if self.transition.shape[2] > 1:         transition_t = t
            if self.state_intercept.shape[1] > 1:    state_intercept_t = t
            if self.selection.shape[2] > 1:          selection_t = t
            if self.state_cov.shape[2] > 1:          state_cov_t = t

        # Initialize object-level pointers to statespace arrays
        self._obs = &self.obs[0, t]
        self._design = &self.design[0, 0, design_t]
        self._obs_intercept = &self.obs_intercept[0, obs_intercept_t]
        self._obs_cov = &self.obs_cov[0, 0, obs_cov_t]
        self._transition = &self.transition[0, 0, transition_t]
        self._state_intercept = &self.state_intercept[0, state_intercept_t]
        self._selection = &self.selection[0, 0, selection_t]
        self._state_cov = &self.state_cov[0, 0, state_cov_t]

        # Initialize object-level pointers to initialization
        if not self.initialized:
            raise RuntimeError("Statespace model not initialized.")
        self._initial_state = &self.initial_state[0]
        self._initial_state_cov = &self.initial_state_cov[0,0]
        self._initial_diffuse_state_cov = &self.initial_diffuse_state_cov[0,0]

        # Create the selected state covariance matrix
        self.select_state_cov(t)

        # Handle missing data
        # Note: this modifies object pointers and _* dimensions
        k_endog = self.select_missing(t)

        # Set dimensions
        self.set_dimensions(k_endog, self.k_states, self.k_posdef)

        # Handle transformations
        self.transform(t, self._previous_t, transform_diagonalize, transform_generalized_collapse)

    cdef void set_dimensions(self, unsigned int k_endog, unsigned int k_states, unsigned int k_posdef):
        self._k_endog = k_endog
        self._k_states = k_states
        self._k_posdef = k_posdef
        self._k_endog2 = k_endog**2
        self._k_states2 = k_states**2
        self._k_posdef2 = k_posdef**2
        self._k_endogstates = k_endog * k_states
        self._k_statesposdef = k_states * k_posdef

    cdef void select_state_cov(self, unsigned int t):
        cdef int selected_state_cov_t = 0

        # ### Get selected state covariance matrix
        if t == 0 or self.selected_state_cov.shape[2] > 1:
            selected_state_cov_t = t
            self._selected_state_cov = &self.selected_state_cov[0, 0, selected_state_cov_t]

            {{prefix}}select_cov(self.k_states, self.k_posdef,
                                       &self.tmp[0,0],
                                       self._selection,
                                       self._state_cov,
                                       self._selected_state_cov)
        else:
            self._selected_state_cov = &self.selected_state_cov[0, 0, 0]

    cdef int select_missing(self, unsigned int t):
        # Note: this assumes that object pointers are already initialized
        # Note: this assumes that transform_... will be done *later*
        cdef int k_endog = self.k_endog

        # Set the current iteration nmissing
        self._nmissing = self.nmissing[t]

        # ### Perform missing selections
        # In Durbin and Koopman (2012), these are represented as matrix
        # multiplications, i.e. $Z_t^* = W_t Z_t$ where $W_t$ is a row
        # selection matrix (it contains a subset of rows of the identity
        # matrix).
        #
        # It's more efficient, though, to just copy over the data directly,
        # which is what is done here. Note that the `selected_*` arrays are
        # defined as single-dimensional, so the assignment indexes below are
        # set such that the arrays can be interpreted by the BLAS and LAPACK
        # functions as two-dimensional, column-major arrays.
        #
        # In the case that all data is missing (e.g. this is what happens in
        # forecasting), we actually set do not change the dimension, but we set
        # the design matrix to the zeros array.
        if self._nmissing == self.k_endog:
            self._select_missing_entire_obs(t)
        elif self._nmissing > 0:
            self._select_missing_partial_obs(t)
            k_endog = self.k_endog - self._nmissing

        # Return the number of non-missing endogenous variables
        return k_endog

    cdef void _select_missing_entire_obs(self, unsigned int t):
        cdef:
            int i, j

        # Design matrix is set to zeros
        for i in range(self.k_states):
            for j in range(self.k_endog):
                self.selected_design[j + i*self.k_endog] = 0.0
        self._design = &self.selected_design[0]

    cdef void _select_missing_partial_obs(self, unsigned int t):
        cdef:
            int i, j, k, l
            int inc = 1
            int design_t = 0
            int obs_cov_t = 0
            int k_endog = self.k_endog - self._nmissing

        k = 0
        for i in range(self.k_endog):
            if not self.missing[i, t]:

                self.selected_obs[k] = self._obs[i]
                self.selected_obs_intercept[k] = self._obs_intercept[i]

                # i is rows, k is rows
                blas.{{prefix}}copy(&self.k_states,
                      &self._design[i], &self.k_endog,
                      &self.selected_design[k], &k_endog)

                # i, k is columns, j, l is rows
                l = 0
                for j in range(self.k_endog):
                    if not self.missing[j, t]:
                        self.selected_obs_cov[l + k*k_endog] = self._obs_cov[j + i*self.k_endog]
                        l += 1
                k += 1
        self._obs = &self.selected_obs[0]
        self._obs_intercept = &self.selected_obs_intercept[0]
        self._design = &self.selected_design[0]
        self._obs_cov = &self.selected_obs_cov[0]

    cdef void transform(self, unsigned int t, unsigned int previous_t, unsigned int transform_diagonalize, unsigned int transform_generalized_collapse) except *:
        # Reset the collapsed loglikelihood
        self.collapse_loglikelihood = 0

        if transform_generalized_collapse and not self._k_endog <= self._k_states:
            k_endog = self.transform_generalized_collapse(t, previous_t)
            # Reset dimensions
            self.set_dimensions(k_endog, self._k_states, self._k_posdef)
        elif transform_diagonalize and not (self.diagonal_obs_cov == 1):
            self.transform_diagonalize(t, previous_t)

    cdef void transform_diagonalize(self, unsigned int t, unsigned int previous_t) except *:
        # Note: this assumes that initialize_object_pointers has *already* been done
        # Note: this assumes that select_missing has *already* been done
        # TODO need unit tests, especially for the missing case
        # TODO need to also transform observation intercept
        cdef:
            int i, j, inc=1
            int obs_cov_t = 0
            int info
            int reset_missing
            int diagonal_obs_cov
            {{cython_type}} * _transform_obs_cov = &self.transform_obs_cov[0, 0]
            {{cython_type}} * _transform_cholesky = &self.transform_cholesky[0, 0]

        if self.obs_cov.shape[2] > 1:
            obs_cov_t = t

        # Handle missing data
        if self.nmissing[t] == self.k_endog:
            return
        reset_missing = 0
        for i in range(self.k_endog):
            reset_missing = reset_missing + (not self.missing[i,t] == self.missing[i,previous_t])

        # If the flag for a diagonal covariancem matrix is not set globally in
        # the model one way or the other, then we need to check
        diagonal_obs_cov = self.diagonal_obs_cov
        if diagonal_obs_cov == -1:
            # We don't need to check for a diagonal covariance matrix each t,
            # except in the following cases:
            if self._diagonal_obs_cov == -1 or t == 0 or self.obs_cov.shape[2] > 1 or reset_missing:
                diagonal_obs_cov = 1
                for i in range(self.k_endog):
                    for j in range(self.k_endog):
                        if i == j:
                            continue
                        if not ({{combined_prefix}}abs(self.obs_cov[i, j, obs_cov_t]) < 1e-9):
                            diagonal_obs_cov = 0
                            break
            # Otherwise, we use whatever value was produced last period
            else:
                diagonal_obs_cov = self._diagonal_obs_cov
        self._diagonal_obs_cov = diagonal_obs_cov
        if diagonal_obs_cov == 1:
            return

        # If we have a non-diagonal obs cov, we need to compute the cholesky
        # decomposition of *self._obs_cov
        if t == 0 or self.obs_cov.shape[2] > 1 or reset_missing:
            # LDL decomposition
            blas.{{prefix}}copy(&self._k_endog2, self._obs_cov, &inc, _transform_cholesky, &inc)
            info = tools._{{prefix}}ldl(_transform_cholesky, self._k_endog)

            # Check for errors
            if info > 0:
                warnings.warn("Positive semi-definite observation covariance matrix encountered at period %d" % t)
            elif info < 0:
                raise np.linalg.LinAlgError('Invalid value in LDL factorization of observation covariance matrix encountered at period %d' % t)

            # Currently both L and D are stored in transform_cholesky
            for i in range(self._k_endog): # i is rows
                for j in range(self._k_endog): # j is columns
                    # Diagonal elements come from the diagonal
                    if i == j:
                        _transform_obs_cov[i + i * self._k_endog] = _transform_cholesky[i + i * self._k_endog]
                    # Other elements are zero
                    else:
                        _transform_obs_cov[i + j * self._k_endog] = 0

                    # Zero out the upper triangle of the cholesky
                    if j > i:
                        _transform_cholesky[i + j * self._k_endog] = 0

                # Convert from L to C simply by setting the diagonal elements to ones
                _transform_cholesky[i + i * self._k_endog] = 1

        # Solve for y_t^*
        # (unless this is a completely missing observation)
        if not self._nmissing == self.k_endog:
            # If we have some missing elements, selected_obs is already populated
            if self._nmissing == 0:
                blas.{{prefix}}copy(&self._k_endog, &self.obs[0,t], &inc, &self.selected_obs[0], &inc)
            lapack.{{prefix}}trtrs("L", "N", "U", &self._k_endog, &inc,
                        _transform_cholesky, &self.k_endog,
                        &self.selected_obs[0], &self._k_endog, &info)

            # Check for errors
            if info > 0:
                raise np.linalg.LinAlgError('Singular factorization of observation covariance matrix encountered at period %d' % t)
            elif info < 0:
                raise np.linalg.LinAlgError('Invalid value in factorization of observation covariance matrix encountered at period %d' % t)

            # Setup the pointer
            self._obs = &self.selected_obs[0]

        # Solve for d_t^*, if necessary
        if t == 0 or self.obs_intercept.shape[1] > 1 or reset_missing:
            blas.{{prefix}}copy(&self._k_endog, self._obs_intercept, &inc, &self.transform_obs_intercept[0], &inc)
            lapack.{{prefix}}trtrs("L", "N", "U", &self._k_endog, &inc,
                        _transform_cholesky, &self.k_endog,
                        &self.transform_obs_intercept[0], &self._k_endog,
                        &info)

        # Solve for Z_t^*, if necessary
        if t == 0 or self.design.shape[2] > 1 or reset_missing:
            blas.{{prefix}}copy(&self._k_endogstates, self._design, &inc, &self.transform_design[0,0], &inc)
            lapack.{{prefix}}trtrs("L", "N", "U", &self._k_endog, &self._k_states,
                        _transform_cholesky, &self.k_endog,
                        &self.transform_design[0,0], &self._k_endog,
                        &info)

            # Check for errors
            if info > 0:
                raise np.linalg.LinAlgError('Singular factorization of observation covariance matrix encountered at period %d' % t)
            elif info < 0:
                raise np.linalg.LinAlgError('Invalid value in factorization of observation covariance matrix encountered at period %d' % t)

        # Setup final pointers            
        self._design = &self.transform_design[0,0]
        self._obs_cov = &self.transform_obs_cov[0,0]
        self._obs_intercept = &self.transform_obs_intercept[0]

    cdef int transform_generalized_collapse(self, unsigned int t, unsigned int previous_t) except *:
        # Note: this assumes that initialize_object_pointers has *already* been done
        # Note: this assumes that select_missing has *already* been done
        # TODO need unit tests, especially for the missing case
        cdef:
            int i, j, inc=1
            int obs_cov_t, design_t
            int info
            int reset_missing
            {{cython_type}} alpha = 1.0
            {{cython_type}} beta = 0.0
            {{cython_type}} gamma = -1.0
            int k_states = self._k_states
            int k_states2 = self._k_states2
            int k_endogstates = self._k_endogstates

        # $y_t^* = \bar A^* y_t = C_t Z_t' H_t^{-1} y_t$  
        # $Z_t^* = C_t^{-1}$  
        # $H_t^* = I_m$  

        # Make sure we have enough observations to perform collapse
        if self.k_endog < self.k_states:
            raise RuntimeError('Cannot collapse observation vector it the'
                               ' state dimension is larger than the dimension'
                               ' of the observation vector.')

        # Adjust for a VAR transition (i.e. design = [#, 0], where the zeros
        # correspond to all states except the first k_posdef states)
        if self.subset_design:
            k_states = self._k_posdef
            k_states2 = self._k_posdef2
            k_endogstates = self._k_endog * self._k_posdef

        # Handle missing data
        if self.nmissing[t] == self.k_endog:
            return self.k_states
        reset_missing = 0
        for i in range(self.k_endog):
            reset_missing = reset_missing + (not self.missing[i,t] == self.missing[i,previous_t])

        # Initialize the transformation
        if self.collapse_obs_cov[0,0] == 0:
            # Set H_t^* to identity
            for i in range(k_states):
                self.collapse_obs_cov[i,i] = 1

            # Make sure we do not have an observation intercept
            if not np.sum(self.obs_intercept) == 0 or self.obs_intercept.shape[2] > 1:
                raise RuntimeError('The observation collapse transformation'
                                   ' does not currently support an observation'
                                   ' intercept.')

        # Perform the Cholesky decomposition of H_t, if necessary
        if t == 0 or self.obs_cov.shape[2] > 1 or reset_missing:
            # Cholesky decomposition: $H = L L'$  
            blas.{{prefix}}copy(&self._k_endog2, self._obs_cov, &inc, &self.transform_cholesky[0,0], &inc)
            # Use LDA=self.k_endog so that we can use the memoryview slicing below
            lapack.{{prefix}}potrf("L", &self._k_endog, &self.transform_cholesky[0,0], &self.k_endog, &info)

            # Check for errors
            if info > 0:
                raise np.linalg.LinAlgError('Non-positive-definite observation covariance matrix encountered at period %d' % t)
            elif info < 0:
                raise np.linalg.LinAlgError('Invalid value in observation covariance matrix encountered at period %d' % t)

            # Calculate the determinant (just the squared product of the
            # diagonals, in the Cholesky decomposition case)
            self.transform_determinant = 1.0
            for i in range(self._k_endog):
                if not self.transform_cholesky[i, i] == 0:
                    self.transform_determinant = self.transform_determinant * self.transform_cholesky[i, i]
            self.transform_determinant = self.transform_determinant**2

        # Get $Z_t \equiv C^{-1}$, if necessary  
        if t == 0 or self.obs_cov.shape[2] > 1 or self.design.shape[2] > 1 or reset_missing:
            # Calculate $H_t^{-1} Z_t \equiv (Z_t' H_t^{-1})'$ via Cholesky solver
            blas.{{prefix}}copy(&self._k_endogstates, self._design, &inc, &self.transform_design[0,0], &inc)
            lapack.{{prefix}}potrs("L", &self._k_endog, &k_states,
                            &self.transform_cholesky[0,0], &self.k_endog,
                            &self.transform_design[0,0], &self._k_endog,
                            &info)

            # Check for errors
            if not info == 0:
                raise np.linalg.LinAlgError('Invalid value in calculation of H_t^{-1}Z matrix encountered at period %d' % t)
        
            # Calculate $(H_t^{-1} Z_t)' Z_t$  
            # $(m \times m) = (m \times p) (p \times p) (p \times m)$
            blas.{{prefix}}gemm("T", "N", &k_states, &k_states, &self._k_endog,
                   &alpha, self._design, &self._k_endog,
                           &self.transform_design[0,0], &self._k_endog,
                   &beta, &self.collapse_cholesky[0,0], &self._k_states)

            # Calculate $(Z_t' H_t^{-1} Z_t)^{-1}$ via Cholesky inversion  
            lapack.{{prefix}}potrf("U", &k_states, &self.collapse_cholesky[0,0], &self.k_states, &info)
            lapack.{{prefix}}potri("U", &k_states, &self.collapse_cholesky[0,0], &self.k_states, &info)

            # Calculate $C_t$ (the upper triangular cholesky decomposition of $(Z_t' H_t^{-1} Z_t)^{-1}$)  
            lapack.{{prefix}}potrf("U", &k_states, &self.collapse_cholesky[0,0], &self.k_states, &info)

            # Check for errors
            if info > 0:
                raise np.linalg.LinAlgError('Non-positive-definite ZHZ matrix encountered at period %d' % t)
            elif info < 0:
                raise np.linalg.LinAlgError('Invalid value in ZHZ matrix encountered at period %d' % t)

            # Calculate $C_t'^{-1} \equiv Z_t$  
            # Do so by solving the system: $C_t' x = I$  
            # (Recall that collapse_obs_cov is an identity matrix)
            blas.{{prefix}}copy(&self._k_states2, &self.collapse_obs_cov[0,0], &inc, &self.collapse_design[0,0], &inc)
            lapack.{{prefix}}trtrs("U", "T", "N", &k_states, &k_states,
                        &self.collapse_cholesky[0,0], &self._k_states,
                        &self.collapse_design[0,0], &self._k_states,
                        &info)

        # Calculate $\bar y_t^* = \bar A_t^* y_t = C_t Z_t' H_t^{-1} y_t$  
        # (unless this is a completely missing observation)
        self.collapse_loglikelihood = 0
        if not self._nmissing == self.k_endog:
            # If we have some missing elements, selected_obs is already populated
            if self._nmissing == 0:
                blas.{{prefix}}copy(&self.k_endog, &self.obs[0,t], &inc, &self.selected_obs[0], &inc)
            # $\\# = Z_t' H_t^{-1} y_t$
            blas.{{prefix}}gemv("T", &self._k_endog, &k_states,
                  &alpha, &self.transform_design[0,0], &self._k_endog,
                          &self.selected_obs[0], &inc,
                  &beta, &self.collapse_obs[0], &inc)
            # $y_t^* = C_t \\#$  
            blas.{{prefix}}trmv("U", "N", "N", &k_states,
                                &self.collapse_cholesky[0,0], &self._k_states,
                                &self.collapse_obs[0], &inc)

            # Get residuals for loglikelihood calculation
            # Note: Durbin and Koopman (2012) appears to have an error in the
            # formula here. They have $e_t = y_t - Z_t \bar y_t^*$, whereas it
            # should be: $e_t = y_t - Z_t C_t' \bar y_t^*$
            # See Jungbacker and Koopman (2014), section 2.5 where $e_t$ is
            # defined. In this case, $Z_t^dagger = Z_t C_t$ where
            # $C_t C_t' = (Z_t' \Sigma_\varepsilon^{-1} Z_t)^{-1}$.
            # 

            # $ \\# = C_t' y_t^*$
            blas.{{prefix}}copy(&k_states, &self.collapse_obs[0], &inc, &self.collapse_obs_tmp[0], &inc)
            blas.{{prefix}}trmv("U", "T", "N", &k_states,
                                &self.collapse_cholesky[0,0], &self._k_states,
                                &self.collapse_obs_tmp[0], &inc)

            # $e_t = - Z_t C_t' y_t^* + y_t$
            blas.{{prefix}}gemv("N", &self._k_endog, &k_states,
                  &gamma, self._design, &self._k_endog,
                          &self.collapse_obs_tmp[0], &inc,
                  &alpha, &self.selected_obs[0], &inc)

            # Calculate e_t' H_t^{-1} e_t via Cholesky solver  
            # $H_t^{-1} = (L L')^{-1} = L^{-1}' L^{-1}$  
            # So we want $e_t' L^{-1}' L^{-1} e_t = (L^{-1} e_t)' L^{-1} e_t$  
            # We have $L$ in `transform_cholesky`, so we want to do a linear  
            # solve of $L x = e_t$  where L is lower triangular
            lapack.{{prefix}}trtrs("L", "N", "N", &self._k_endog, &inc,
                        &self.transform_cholesky[0,0], &self.k_endog,
                        &self.selected_obs[0], &self._k_endog,
                        &info)

            # Calculate loglikelihood contribution of this observation

            # $e_t' H_t^{-1} e_t = (L^{-1} e_t)' L^{-1} e_t = \sum_i e_{i,t}**2$  
            self.collapse_loglikelihood = 0
            for i in range(self._k_endog):
                self.collapse_loglikelihood = self.collapse_loglikelihood + self.selected_obs[i]**2
            
            # (p-m) log( 2*pi) + log( |H_t| )
            self.collapse_loglikelihood = (
                self.collapse_loglikelihood +
                (self._k_endog - k_states)*{{combined_prefix}}log(2*NPY_PI) + 
                {{combined_prefix}}log(self.transform_determinant)
            )

            # -0.5 * ...
            self.collapse_loglikelihood = -0.5 * self.collapse_loglikelihood

        # Set pointers
        self._obs = &self.collapse_obs[0]
        self._design = &self.collapse_design[0,0]
        self._obs_cov = &self.collapse_obs_cov[0,0]

        # TODO can I replace this with k_states? I think I should be able to
        return self._k_states

# ### Selected covariance matrice
cdef int {{prefix}}select_cov(int k, int k_posdef,
                              {{cython_type}} * tmp,
                              {{cython_type}} * selection,
                              {{cython_type}} * cov,
                              {{cython_type}} * selected_cov):
    cdef:
        {{cython_type}} alpha = 1.0
        {{cython_type}} beta = 0.0

    # Only need to do something if there is a covariance matrix
    # (i.e k_posdof == 0)
    if k_posdef > 0:

        # #### Calculate selected state covariance matrix  
        # $Q_t^* = R_t Q_t R_t'$
        # 
        # Combine a selection matrix and a covariance matrix to get
        # a simplified (but possibly singular) "selected" covariance
        # matrix (see e.g. Durbin and Koopman p. 43)

        # `tmp0` array used here, dimension $(m \times r)$  

        # TODO this does not require two ?gemm calls, since we know that it
        # is just selection rows and columns of the Q matrix

        # $\\#_0 = 1.0 * R_t Q_t$  
        # $(m \times r) = (m \times r) (r \times r)$
        blas.{{prefix}}gemm("N", "N", &k, &k_posdef, &k_posdef,
              &alpha, selection, &k,
                      cov, &k_posdef,
              &beta, tmp, &k)
        # $Q_t^* = 1.0 * \\#_0 R_t'$  
        # $(m \times m) = (m \times r) (m \times r)'$
        blas.{{prefix}}gemm("N", "T", &k, &k, &k_posdef,
              &alpha, tmp, &k,
                      selection, &k,
              &beta, selected_cov, &k)

{{endfor}}